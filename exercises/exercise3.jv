pipeline EconomicMetricsPipeline {

    // Defining custom value types with constraints
    valuetype set_Difference oftype decimal {
        constraints: [set_boundary];
    }

    valuetype set_positive oftype decimal {
        constraints: [set_range];
    }

    constraint set_boundary oftype RangeConstraint {
        lowerBound: 0;
        upperBound: 1;
    }

    constraint set_range oftype RangeConstraint {
        lowerBound: 0;
    }
    

    // Downloading the file from a specified URL
    block set_data_downloader oftype HttpExtractor {
        url: 'https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx';
        retries: 3;
    }

    block FileInterpreter oftype XLSXInterpreter { }

    // Selecting the sheet to be processed
    block pick_targeted_sheet oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    // Extracting the specified cell range for data processing
    block set_cell_range oftype CellRangeSelector {
        select: range P2:S45;
    }

    // Renaming columns for clarity
    block HeaderRenamer oftype CellWriter {
        at: range A1:D1;
        write: [
            'Country Code',
            'Economy',
            'GDP per Capita',
            'Bond Issuance Share'
        ];
    }

    // Parsing GDP-related data with specified column types
    block GDPDataParser oftype TableInterpreter {
        header: true;
        columns: [
            'Country Code' oftype CountryCodeAlpha3,
            'GDP per Capita' oftype set_positive,
        ];
    }

    // Parsing Bond-related data with specified column types
    block BondDataParser oftype TableInterpreter {
        header: true;
        columns: [
            'Country Code' oftype CountryCodeAlpha3,
            'Bond Issuance Share' oftype set_positive,
        ];
    }

    // Loading GDP data into the SQLite database
    block GDPSaver oftype SQLiteLoader {
        table: 'gdpPerCapita';
        file: 'economic_metrics.sqlite';
    }

    // Loading Bond data into the SQLite database
    block BondSaver oftype SQLiteLoader {
        table: 'bondIssuance';
        file: 'economic_metrics.sqlite';
    }

    set_data_downloader
        -> FileInterpreter
        -> pick_targeted_sheet
        -> set_cell_range
        -> HeaderRenamer;

    HeaderRenamer
        -> GDPDataParser
        -> GDPSaver;

    HeaderRenamer
        -> BondDataParser
        -> BondSaver;
}
